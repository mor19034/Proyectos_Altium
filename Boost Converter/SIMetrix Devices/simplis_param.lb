** Copyright Catena Software/Transim Technology Corporation
** 2004 All rights reserved
** SIMetrix/SIMPLIS release 5.0 Model Library
**
** These models are believed to be accurate but neither
** Catena Software Ltd. nor Transim Technology Corporation
** offer any guarantee as to their fitness for any purpose nor do they
** accept any responsibility for damage caused directly or indirectly
** by their use.
**
**
** SIMPLIS Parameterised devices
**
** This line marks this library as a system library
** Models in this library will not appear in the
** "User Models" or "New Models" categories
.systemlib
**

.SIMULATOR SIMPLIS



.SUBCKT PERIODIC_OP  1 3 vars:
+ VREF=2.5 VOL=0 VOH=5 HYSTWD=0.002 DIVIDE_BY_2=0 IC=0 DELAY=0.0 TRIG_COND='0_TO_1'
*#LABELS ,Ref. Voltage:REAL,Output Low Voltage,Output High Voltage,Hysteresis::1e-12,Divide By Two:BOOL,Initial Condition of Output (used in Divide by Two):LIST:0|1,Delay::0,Input Trigger Condition (Used in Divide by Two):LIST:'0_TO_1'|'1_TO_0'
*#ICDEFS,!D_CYCLE:IC

 .NODE_MAP IN 1
 .NODE_MAP OUT 3

.if {DIVIDE_BY_2 == 0} then
					* Normal usage, use a comparator
  !D_CYCLE 3 0 1 302 M1M IC={IC}
  VREF 302 0 DC {VREF}
  .MODEL M1M COMP RIN=10MEG ROUT=50 VOL={VOL} VOH={VOH}  HYSTWD={HYSTWD} DELAY={DELAY}

.else
					* Divide by 2, use a Toggle-FF
  .var VTH_PLUS={VREF+5*HYSTWD}
  .if {TRIG_COND != '1_TO_0'} then
    .var TRIG_COND = '0_TO_1'
  .endif
  !D_CYCLE 3 204 0 303 1 M1M IC={IC}
  VREF 303 0 DC {VTH_PLUS}
  .MODEL M1M CLK_TFF RIN=10MEG ROUT=50 LOGIC=POS TRIG_COND={TRIG_COND}
  + TH={VREF} HYSTWD={HYSTWD} VOL={VOL} VOH={VOH} DELAY={DELAY}

.endif

.ENDS PERIODIC_OP


** There is also a SIMetrix compatible version of this
** Also, the symbol simplis_prim_vcswitch implements the same thing in SIMPLIS only
** IMPORTANT - the ic_vc_switch.sxscr script makes some assumptions about this model
** Please read comment in same if editing.
.SUBCKT VC_SWITCH 1 2 3 4 vars: ROFF=1MEG RON=1 THRESHOLD=2 HYSTWD=0.1 IC=0
*#LABELS ,Off Resistance::1n,On Resistance::1n,Threshold,Hysteresis::1e-12,Initial Condition:LIST:0 Off|1 On
*#ICDEFS,S1:IC

 .NODE_MAP P 1
 .NODE_MAP N 2
 .NODE_MAP CP 3
 .NODE_MAP CN 4

.if {RON>ROFF}
    .var MODEL_RON={ROFF}
    .var MODEL_ROFF={RON}
    .var S_IC={(['CLOSE', 'OPEN'])[IC]}
    .var S_LOGIC='NEG'
.else
    .var MODEL_RON={RON}
    .var MODEL_ROFF={ROFF}
    .var S_IC={(['OPEN', 'CLOSE'])[IC]}
    .var S_LOGIC='POS'
.endif

S1 1 2 3 4 SW IC={S_IC}
.MODEL SW VCSW ROFF={MODEL_ROFF} RON={MODEL_RON} TH={THRESHOLD} HYSTWD={HYSTWD} LOGIC={S_LOGIC}
.ENDS


.SUBCKT SIMPLIS_VC_SWITCH 1 2 3 4 vars: ROFF=1MEG RON=1 THRESHOLD=2 HYSTWD=0.1 IC='OPEN' LOGIC='POS'
*#LABELS ,Off Resistance::10n,On Resistance::1n,Threshold,Hysteresis::1e-12,Initial Condition:LIST:'OPEN'|'CLOSE',Logic:LIST:'POS'|'NEG'
*#ICDEFS,S1:IC

 .NODE_MAP P 1
 .NODE_MAP N 2
 .NODE_MAP CP 3
 .NODE_MAP CN 4

.if {RON>ROFF}
    .error "The value of On Resistance must be lower than the Off Resistance."
.endif

S1 1 2 3 4 SW IC={IC}
.MODEL SW VCSW ROFF={ROFF} RON={RON} TH={THRESHOLD} HYSTWD={HYSTWD} LOGIC={LOGIC}
.ENDS


.SUBCKT SIMPLIS_CC_SWITCH 1 2 3 4 vars: ROFF=1MEG RON=1 THRESHOLD=0 HYSTWD=1M IC='OPEN' LOGIC='POS'
*#LABELS ,Off Resistance::10n,On Resistance::1n,Threshold,Hysteresis::1e-12,Initial Condition:LIST:'OPEN'|'CLOSE',Logic:LIST:'POS'|'NEG'
*#ICDEFS,S1:IC

 .NODE_MAP P 1
 .NODE_MAP N 2
 .NODE_MAP CP 3
 .NODE_MAP CN 4

.if {RON>ROFF}
    .error "The value of On Resistance must be lower than the Off Resistance."
.endif

VISENSE 3 4 0
S1 1 2 VISENSE SW IC={IC}
.MODEL SW ICSW ROFF={ROFF} RON={RON} TH={THRESHOLD} HYSTWD={HYSTWD} LOGIC={LOGIC}
.ENDS


** Electrolytic capacitor - Level 1 to 3
.subckt ELEC_CAP_L13 1 9 vars: LEVEL=1 CC=1000u RSH_CC=1meg IC=0 RESR=10m LESL=100n USEIC=0
*#LABELS ,Level (1-3):INTEGER:1|3,Capacitance,Leakage Resistance,Initial Condition,ESR (Level>1),ESL (Level=3),Use Initial Condition:BOOL
*#ICDEFS,ENABLE:USEIC,C1:IC

 .NODE_MAP P 1
 .NODE_MAP N 9

.if {LEVEL==1}
    .var capnode = 1
.else
    .var capnode = 2
.endif

.if {USEIC}
    C1 {capnode} 9 {CC} IC={IC}
.else
    C1 {capnode} 9 {CC}
.endif

RSH {capnode} 9 {RSH_CC}

.if {LEVEL==2}
    RESR 1 2 {RESR}
.else
    .if {LEVEL==3}
        RESR 2 3 {RESR}
        LESL 1 3 {LESL}
    .endif
.endif

.ends


** Electrolytic capacitor - Level 4 to 5
.subckt ELEC_CAP_L45 1 9 vars: LEVEL=4 CC_LF=1000u CC_HF=1u RSH_CC_LF=1meg RSH_CC_HF=1meg RESR_LF=50m RESR_HF=10m LESL=100n IC=0 USEIC=0
*#LABELS ,Level (4-5):INTEGER:4|5,Capacitance LF,Capacitance HF,Leakage Res. LF,Leakage Res. HF,ESR LF,ESR HF,ESL (Level=5),Initial Condition,Use Initial Condition:BOOL
*#ICDEFS,ENABLE:USEIC,CC_LF:IC

 .NODE_MAP P 1
 .NODE_MAP N 9

.if {LEVEL==4}
    .var node = 1
.else
    .var node = 4
    LESL 1 4 {LESL}
.endif

.if {USEIC}
    CC_LF 2 9 {CC_LF} IC={IC}
    CC_HF 3 9 {CC_HF} IC={IC}
.else
    CC_LF 2 9 {CC_LF}
    CC_HF 3 9 {CC_HF}
.endif

RSH_CC_LF 2 9 {RSH_CC_LF}
RSH_CC_HF 3 9 {RSH_CC_HF}

RESR_LF {node} 2 {RESR_LF}
RESR_HF {node} 3 {RESR_HF}

.ends


**** SIMPLIS Primitive Q Switch implemented as a voltage controlled Q switch
****
.SUBCKT SIMPLIS_VC_QSWITCH 1 2 3 4 vars:
+ ROFF=1MEG RSAT=1 TH=2 HYSTWD=0.1
+ VSAT=0 GAIN=10 IC='OPEN' LOGIC='POS'
+ IFLOW='Bi-directional' POLARITY='Positive' LEVEL=1
*#LABELS ,Off Resistance::10n,Sat. Resistance::1n,Threshold,Hysteresis::1e-12,Sat. Voltage,Gain::1n,Initial Condition:LIST:'OPEN'|'CLOSE',Logic:LIST:'POS'|'NEG',Current Flow:LIST:'Bi-directional'|'Uni-directional',Polarity:LIST:'Positive'|'Negative',Model Level:LIST:1|2
*#ICDEFS,Q1:IC

 .NODE_MAP P 1
 .NODE_MAP N 2
 .NODE_MAP CP 3
 .NODE_MAP CN 4

.if {RSAT>ROFF}
    .error "The value of Sat. Resistance must be lower than the Off Resistance."
.endif

.if {IFLOW=='BI-DIRECTIONAL' && VSAT!=0}
    .error "IFLOW=Bi-directional and a nonzero VSAT parameter are not compatible."
.endif

.if {POLARITY=='Positive' && VSAT < 0}
    .error "VSAT must be positive if POLARITY is Positive'
.endif

.if {POLARITY=='Negative' && VSAT > 0}
    .error "VSAT must be negative if POLARITY is negative'
.endif

.if {POLARITY=='Positive'}
    .var MTYPE='VCQPOS'
.else
    .var MTYPE='VCQNEG'
.endif

.if {IFLOW=='BI-DIRECTIONAL'}
    .var IFLOW='BIDIR'
.else
    .var IFLOW='UNIDIR'
.endif

Q1 1 2 3 4 VC_QSW IC={IC}
.MODEL VC_QSW {MTYPE} TH={TH} HYSTWD={HYSTWD} RSAT={RSAT} ROFF={ROFF}
+ VSAT={VSAT} GAIN={GAIN} LOGIC={LOGIC} LEVEL={LEVEL} IFLOW={IFLOW}
.ENDS


**** SIMPLIS Primitive Q Switch implemented as a current controlled Q switch
****
.SUBCKT SIMPLIS_CC_QSWITCH 1 2 3 4 vars:
+ ROFF=1MEG RSAT=1 TH=0 HYSTWD=1m
+ VSAT=0 GAIN=10 IC='OPEN' LOGIC='POS'
+ IFLOW='Bi-directional' POLARITY='Positive' LEVEL=1
*#LABELS ,Off Resistance::10n,Sat. Resistance::1n,Threshold,Hysteresis::1e-12,Sat. Voltage,Gain::1n,Initial Condition:LIST:'OPEN'|'CLOSE',Logic:LIST:'POS'|'NEG',Current Flow:LIST:'Bi-directional'|'Uni-directional',Polarity:LIST:'Positive'|'Negative',Model Level:LIST:1|2
*#ICDEFS,Q1:IC

 .NODE_MAP P 1
 .NODE_MAP N 2
 .NODE_MAP CP 3
 .NODE_MAP CN 4

.if {RSAT>ROFF}
    .error "The value of Sat. Resistance must be lower than the Off Resistance."
.endif

.if {IFLOW=='Bi-directional' && VSAT!=0}
    .error "IFLOW=BI-DIRECTIONAL and a nonzero VSAT parameter are not compatible."
.endif

.if {POLARITY=='Positive' && VSAT < 0}
    .error "VSAT must be positive if POLARITY is Positive'
.endif

.if {POLARITY=='Negative' && VSAT > 0}
    .error "VSAT must be negative if POLARITY is negative'
.endif

.if {POLARITY=='Positive'}
    .var MTYPE='ICQPOS'
.else
    .var MTYPE='ICQNEG'
.endif

.if {IFLOW=='BI-DIRECTIONAL'}
    .var IFLOW='BIDIR'
.else
    .var IFLOW='UNIDIR'
.endif

VISENSE 3 4 0
Q1 1 2 VISENSE IC_QSW IC={IC}
.MODEL IC_QSW {MTYPE} TH={TH} HYSTWD={HYSTWD} RSAT={RSAT} ROFF={ROFF}
+ VSAT={VSAT} GAIN={GAIN} LOGIC={LOGIC} LEVEL={LEVEL} IFLOW={IFLOW}
.ENDS


